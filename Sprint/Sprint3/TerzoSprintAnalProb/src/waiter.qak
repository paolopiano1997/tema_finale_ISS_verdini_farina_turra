System tearoom

mqttBroker "localhost" : 1883 eventTopic "event/topic"

//Movements messages
Dispatch moveto : moveto(X,Y)
Dispatch done : done(X,Y)

//Init messages
Request start : start(S)
Reply ready : ready(V)

//Event for maxstaytime
Dispatch timePassed : timePassed(N)

//Get remaining time
Request getRemainingTime : getRemainingTime(N)
Reply remainingTime : remainingTime(T)


//Dispatch for maxstaytimer
Dispatch doTimerAction : doTimerAction(N,A)
Dispatch stopstaytimer : stopstaytimer(S)

Request     start  : start(S)
Reply 		ready  : ready(V)
Request 	step       : step( TIME )
Reply   	stepdone   : stepdone(V)
Dispatch 	cmd        : cmd(MOVE)
Reply   	stepfail   : stepfail(DURATION, CAUSE)


Dispatch starttimer : starttimer(T)
Dispatch endtime : endtime(T)

Dispatch setTableState : setTableState(N,S)

Request tableState : tableState(N)
Reply state : state(N,S)

Request stoptimer : stoptimer(T)
Reply okStop : okStop(T)

Dispatch gotodirty : gotodirty(G)
Dispatch gotoundirty : gotoundirty(G)
Dispatch gotosanitized : gotosanitized(G)

//Engine
Dispatch stopengine : stopengine(S)

//Client and tearoom messages
Request enter : enter(ID)
Reply accept : accept(C)
//Inform
Reply inform : inform(T)
Dispatch drinkready : drinkready(ID,O)
Dispatch clientready : clientready(ID)
Dispatch paymentready : paymentready(ID)
Dispatch order : order(ID, O)
Dispatch end : end (E)

Dispatch starttimer : starttimer(T)
Dispatch endtime : endtime(T)

Dispatch startcleaner : startcleaner(T)
Dispatch cleanerdone : cleanerdone(C)
Dispatch stopcleaner : stopcleaner(C)

Dispatch occupy  : occupy(N,ID)
Dispatch release : release(N)
Dispatch clean   : clean(N)

Request isClean : isClean(N)
Reply isCleanDone : isCleanDone(N,E)

Request tableClean : tableClean(N)
Reply table : table(N)

Request getTable : getTable(ID)
Reply tableId : tableId(N)

Request isTableStopped : isTableStopped(T)
Reply isTableStoppedDone : isTableStoppedDone(E)

Dispatch gotohome : gotohome(G)

Dispatch gotoclean : gotoclean(G)
Dispatch gotoclean2 : gotoclean2(G)
Dispatch gotoclean3 : gotoclean3(G)

Dispatch gototable1 : gototable1(T)
Dispatch gototable2 : gototable2(T)

Request tableState : tableState(N)
Reply state : state(N,S)

Dispatch timeroff : timeroff(T) 

//SMARTBELL
Dispatch reject : reject(R)

Context ctxbasicrobot     ip [host="localhost"   port=8020]

Context ctxtearoom   ip [host="127.0.0.1" port=8050]  

ExternalQActor basicrobot  context ctxbasicrobot

QActor waitermind context ctxtearoom{
[#
	val CollectTime = 3000L

	//Posizione corrente da raggiungere
	var CurMoveX = 0
	var CurMoveY = 0

	//ID corrente del cliente
	var CurCID = 0

	//Table corrente e table da pulire corrente
	var CurTable = 0
	var CurTableClean = 0

	//Se un cliente suona e non ci sono posti, lo salviamo qua
	var CurEnterCID = 0

	//Ordine corrente
	var CurOrder = ""
	
	//Tempo rimanente minimo clienti
	var RemCTime = 15 //Secondi
	
#]
	State s0 initial {
		discardMsg Off
		solve( consult("tearoomkb.pl")	 )
		println("waitermind   |||   init")
		request waiterengine -m start : start(id)
	}
	Transition t0 whenReply ready -> home
	
	State reachhome{
		println("waitermind   |||   reachhome")
		solve(pos(home, X, Y))
		ifSolved{
			[# 
			   CurMoveX = getCurSol("X").toString().toInt()  
 			   CurMoveY = getCurSol("Y").toString().toInt()  
 			#] 
 			forward waiterengine -m moveto : moveto($CurMoveX,$CurMoveY)
		}
	}
	Transition t0   whenMsg done -> home
					whenRequest enter -> accept
					whenMsg timePassed -> reachTableCollect
				  	whenMsg clientready -> take
				  	whenMsg drinkready -> reachBarman
				  	whenMsg paymentready -> reachTableCollect


	State home{
		println("waitermind   |||   home")
		updateResource [# "home" #]
	}
	Transition t0 whenRequest enter -> accept
				  whenMsg clientready -> take
				  whenMsg timePassed -> timeEndTable
				  whenMsg drinkready -> reachBarman
				  whenMsg paymentready -> reachTableCollect
				  whenMsg end -> endwork
				  
	State accept {
		println("waitermind   |||   accept")
		printCurrentMessage
		onMsg(enter : enter(ID)){
			[# CurCID = payloadArg(0).toString().toInt() #]
			request teatables -m tableClean : tableClean(n)
		}
	}
	Transition t0 whenReply table -> checkTables
	
	State checkTables{
		printCurrentMessage
		println("waitermind   |||   checkTables")
		onMsg(table : table(N)){
			[# CurTable = payloadArg(0).toString().toInt() #]
		}
		if[# CurTable != 0 #]{
			forward waiterengine -m stopengine : stopengine(stop)
			forward waitercleaner -m stopcleaner : stopcleaner(stop)
			delay 400
			println("waitermind   |||   tableclean=$CurTable")
			replyTo enter with accept : accept($CurTable)
			forward teatables -m occupy : occupy($CurTable, $CurCID)
		}
		else{
			println("waitermind   |||   accept failed, $CurCID")
			request maxstaytimer -m getRemainingTime : getRemainingTime(time)
			[# CurEnterCID = CurCID #] //save enter client request
			[# CurCID = 0 #]  //no client
 		}
	}
	Goto reachEntranceDoor if [# CurCID != 0 #] else checkInform
	
	State checkInform{
		println("waitermind   |||   checkInform")
	}
	Transition t0 whenReply remainingTime -> inform
	
	State inform{
		println("waitermind   |||   inform")
		onMsg(remainingTime : remainingTime(T)){
			[# RemCTime = payloadArg(0).toString().toInt() #]
			replyTo enter with inform : inform( $RemCTime ) 
		}
	}
	Goto checkCleanHome
	
	State reachEntranceDoor {
		updateResource[# "reachingEntranceDoor" #]
		println("waitermind   |||   reachEntranceDoor")
		printCurrentMessage
		//updateResource[# "reachEntranceDoor" #]
		solve(pos(entrancedoor, X, Y))
		ifSolved{
			[# 
			   CurMoveX = getCurSol("X").toString().toInt()  
 			   CurMoveY = getCurSol("Y").toString().toInt()  
 			#] 
			forward waiterengine -m moveto : moveto($CurMoveX,$CurMoveY)
		}
	}
	Transition t0 whenMsg done -> convoyToTable
	
	
	State convoyToTable{
		updateResource[# "convoyToTable$CurTable" #]
		println("waitermind   |||   convoyToTable$CurTable")
		solve(pos("teatable$CurTable",X,Y))
		ifSolved{
			[# 
			   CurMoveX = getCurSol("X").toString().toInt()  
 			   CurMoveY = getCurSol("Y").toString().toInt()  
 			#] 
			forward waiterengine -m moveto : moveto ($CurMoveX,$CurMoveY)
			forward teatables -m occupy : occupy($CurTable)
			forward maxstaytimer -m doTimerAction : doTimerAction($CurTable,start)
		}
		println("waitermind   |||   table1 state occupied")
	}
	Transition t0 whenMsg done -> checkCleanHome
	
	
	State checkCleanHome{
		printCurrentMessage
		[# readLine() #]
		println("waitermind   |||   checkClean")
		request waitercleaner -m isTableStopped : isTableStopped(isStopped)
	}
	Transition t0 whenReply isTableStoppedDone -> checkIsTableStopped

	State checkIsTableStopped{
		println("waitermind   |||   checkIsTableStopped")
		onMsg( isTableStoppedDone : isTableStoppedDone(E)){
			if [# payloadArg(0)=="0" #]{ //0 non è interrotto nessun clean
				forward waitermind -m gotohome : gotohome(go)
				[# CurTableClean = 0 #]
			}
			else{
				if[# payloadArg(0)=="1" #]{
					[# CurTableClean = 1 #]
				}
				else{
					[# CurTableClean = 2 #]
				}
			}
		}
	}
	Transition t0 whenTime 200 -> reachTableCleanStopped
			whenMsg gotohome -> reachhome
				
	
	State reachTableCleanStopped{
		println("waitermind   |||   reachTable1CleanStopped")
		solve(pos("teatable$CurTableClean",X,Y))
		ifSolved{
			[#
			   CurMoveX = getCurSol("X").toString().toInt()
 			   CurMoveY = getCurSol("Y").toString().toInt()
 			#]
 			forward waiterengine -m moveto : moveto ($CurMoveX,$CurMoveY)
 		}
	}
	Transition t0 whenMsg done -> cleanTable
	
	State take{
		forward waiterengine -m stopengine : stopengine(stop)
		forward waitercleaner -m stopcleaner : stopcleaner(stop)
		delay 400
		println("waitermind   |||   take")
		updateResource [# "take" #]
		onMsg(clientready : clientready(ID)){
			[#
				CurCID = payloadArg(0).toString().toInt()
			#]
		}
		request teatables -m getTable : getTable($CurCID)
	}
	Transition t0 whenReply tableId -> checkTake
		
	
	State checkTake{
		onMsg(tableId : tableId(N)){
			[# CurTable = payloadArg(0).toString().toInt() #]
			forward maxstaytimer -m doTimerAction : doTimerAction($CurTable,stop)
			solve(pos("teatable$CurTable",X,Y))
			ifSolved{
				[# 
				   CurMoveX = getCurSol("X").toString().toInt()  
	 			   CurMoveY = getCurSol("Y").toString().toInt()  
	 			#] 
	 			forward waiterengine -m moveto : moveto ($CurMoveX,$CurMoveY)
 			}
		}
	}
	Transition t0 whenMsg done -> waitOrder
	
	
	State waitOrder{
		println("waitermind   |||   waitOrder")
	}
	Transition t0 whenMsg order -> transmit
	
	State transmit{
		[# readLine() #]
		println("waitermind   |||   transmit")
		updateResource [# "transmit" #]
		onMsg(order : order(ID,O)){
			forward barman -m order : order($CurCID,payloadArg(0))
		}
	}
	Goto checkCleanHome
	
	State reachBarman{
		forward waiterengine -m stopengine : stopengine(stop)
		forward waitercleaner -m stopcleaner : stopcleaner(stop)
		delay 400
		updateResource[# "reachBarman" #]
		println("waitermind   |||   reachBarman")
		onMsg(drinkready : drinkready(ID,O)){
			[# CurCID = payloadArg(0).toString().toInt() #]
		}
		solve(pos(barman,X, Y))
		ifSolved{
			[# 
			   CurMoveX = getCurSol("X").toString().toInt()  
 			   CurMoveY = getCurSol("Y").toString().toInt()  
 			#] 
 			forward waiterengine -m moveto : moveto ($CurMoveX,$CurMoveY)
		}
	}
	Transition t0 whenMsg done -> serve
	
	State serve{
		[# readLine() #]
		println("waitermind   |||   serve")
		request teatables -m getTable : getTable($CurCID)
	}
	Transition t0 whenReply tableId -> checkTableId
	
	State checkTableId{
		onMsg(tableId : tableId(N)){
			[# CurTable = payloadArg(0).toString().toInt() #]
			forward maxstaytimer -m doTimerAction : doTimerAction($CurTable,start)
			solve(pos("teatable$CurTable",X,Y))
			ifSolved{
				[# 
				   CurMoveX = getCurSol("X").toString().toInt()  
	 			   CurMoveY = getCurSol("Y").toString().toInt()  
	 			#] 
	 			forward waiterengine -m moveto : moveto ($CurMoveX,$CurMoveY)
	 		}
		}
	}
	Transition t0 whenMsg done -> checkCleanHome
	
	State timeEndTable{
		onMsg(timePassed : timePassed(N)){
			[# CurTable = payloadArg(0).toString().toInt() #]
		}
	}
	Goto checkCollect
	
	State reachTableCollect {
		forward waiterengine -m stopengine : stopengine(stop)
		forward waitercleaner -m stopcleaner : stopcleaner(stop)
		delay 400
		println("waitermind   |||   reachTableCollect")
		onMsg(paymentready : paymentready(ID)){
			[# CurCID = payloadArg(0).toString().toInt() #]
		}
		request teatables -m getTable : getTable($CurCID)
	}
	Transition t0 whenReply tableId -> checkCollect

	State checkCollect{
		onMsg(tableId : tableId(N)){
			ifSolved {
				[# CurTable = payloadArg(0).toString().toInt() #]
			}
 		}
 		forward maxstaytimer -m doTimerAction : doTimerAction($CurTable,end)
		updateResource[# "reachTable$CurTable" #]
		solve(pos("teatable$CurTable",X,Y))
		ifSolved{
			[#
			   CurMoveX = getCurSol("X").toString().toInt()  
 			   CurMoveY = getCurSol("Y").toString().toInt()  
 			#] 
 			forward waiterengine -m moveto : moveto ($CurMoveX,$CurMoveY)
 		}
	}
	Transition t0 whenMsg done -> collect
	
	State reachTableClean {
		[# readLine() #]
		println("waitermind   |||   reachTable")
		updateResource[# "reachTableClean" #]
		[# CurTableClean = CurTable #]
		solve(pos("teatable$CurTable",X,Y))
		ifSolved{
			[# 
			   CurMoveX = getCurSol("X").toString().toInt()  
 			   CurMoveY = getCurSol("Y").toString().toInt()  
 			#] 
 			forward waiterengine -m moveto : moveto ($CurMoveX,$CurMoveY)
 		}
	}
	Transition t0 whenMsg done -> cleanTable
	
	State collect {
		println("waitermind   |||   collect")
		updateResource[# "collect" #]
		delayVar CollectTime
	}
	Goto convoyToExitDoor
	
	State convoyToExitDoor{
		println("waitermind   |||   convoyToExitDoor")
		updateResource[# "convoyToExitDoor" #]
		[# CurCID = 0 #]
		solve(pos(exitdoor, X, Y))
		ifSolved{
			[# 
			   CurMoveX = getCurSol("X").toString().toInt()  
 			   CurMoveY = getCurSol("Y").toString().toInt()  
 			#] 
 			forward waiterengine -m moveto : moveto ($CurMoveX,$CurMoveY)
 		}
		forward teatables -m release : release($CurTable)
	}
	Transition t0 whenMsg done -> reachTableClean
	
	State cleanTable{
		updateResource[# "cleanTable$CurTableClean" #]
		println("waitermind   |||   cleanTable$CurTableClean")
		forward waitercleaner -m startcleaner : startcleaner($CurTableClean)
	}
	Transition t0 whenMsg cleanerdone -> checkQueue
				whenRequest enter -> accept
				whenMsg clientready -> take
				whenMsg timePassed -> reachTableCollect
				whenMsg drinkready -> reachBarman
				whenMsg paymentready -> reachTableCollect
	
	State checkQueue{
		println("waitermind   |||   checkQueue")
		if[# CurEnterCID != 0 #]{
			[# 
				CurCID = CurEnterCID
				CurEnterCID = 0
			#]	
			request teatables -m tableClean : tableClean(n)
		}
		else{
			forward waitermind -m gotohome : gotohome(go)
		}
	}
	Transition t0 whenReply table -> checkTables
				whenMsg gotohome -> reachhome
	
	State endwork{
		println("waitermind   |||   end")
		terminate 0
	}
}

QActor timer context ctxtearoom {
	[# 
		var Time: Long = 0
		var StartTime : Long = 0	
		var Duration : Long	= 0
	#]
	State s0 initial {
		println("timer   |||   init")
	}
	Goto wait
	
	State wait {
		printCurrentMessage
		println("timer   |||   wait")
	}
	Transition t0 whenMsg starttimer -> start
	
	State start{
		println("timer   |||   start")
		printCurrentMessage
		onMsg(starttimer : starttimer(T)) {
			[# Time = payloadArg(0).toLong() #]
			memoCurrentTime StartTime
		}
	}
	Transition t0 whenTimeVar Time -> timeFinish
				whenRequest stoptimer -> stop
				
	State stop{
		setDuration Duration from StartTime
		replyTo stoptimer with okStop : okStop($Duration)
	}
	Goto wait
	
	State timeFinish{
		forward waitercleaner -m endtime : endtime(endtime)
	}
	Goto wait
}

QActor waitercleaner context ctxtearoom {

	[#
		var Table = ""
		val	Cleantime = 3000L
		var Clean = Cleantime
		var TableState = ""
		var CurState = "dirty"
	#]
	State s0 initial{// Accettazione richieste/dispatch
		println("waitercleaner   |||   init")
	}
	Goto wait
	
	State wait {
		onMsg(isTableStopped : isTableStopped(T)){
			replyTo isTableStopped with isTableStoppedDone : isTableStoppedDone(0)
		}
		else{
			println("waitercleaner   |||   wait")
		}
	}
	Transition t0 whenMsg startcleaner		 -> preDirty
					whenMsg stopcleaner -> wait
				  whenRequest isTableStopped -> wait

	State timerStop{
		println("waitercleaner   |||   timerStop")
		request timer -m stoptimer : stoptimer(stop)
	}
	Transition t0 whenReply okStop -> stop
	
	State stop {
		println("waitercleaner   |||   stop")
		updateResource[# "cleanStopped" #]
		onMsg(okStop : okStop(T)){
			[# Clean = Cleantime - payloadArg(0).toLong() #]
		}
	}
	Transition t0 whenMsg startcleaner -> preStart
				whenRequest isTableStopped -> replyTableStop
				
	State replyTableStop{
		println("waitercleaner   |||   replyTableStop")
		replyTo isTableStopped with isTableStoppedDone : isTableStoppedDone($Table)
	}
	Goto stop
	
	State preStart{
		println("waitercleaner   |||   preStart")
		updateResource[# "nonStopped" #]
		onMsg(startcleaner : startcleaner(T)){
			[# TableState = payloadArg(0) #]
			request teatables -m tableState : tableState($TableState)
		}
	}
	Transition t0 whenReply state -> checkState
	
	State checkState {
		println("waitercleaner   |||   checkState")
		onMsg(state : state(N,S)){
			if[# payloadArg(0) == "dirty" #]{
				forward waitercleaner -m gotodirty : gotodirty(go)
			}
			else{
				if[# payloadArg(0)=="undirty" #]
				{
					forward waitercleaner -m gotoundirty : gotoundirty(go)
				}
				else{
					forward waitercleaner -m gotosanitized : gotosanitized(go)
				}
			}
		}
	}
	Transition t0 whenMsg gotodirty -> cleanDirty
					whenMsg gotoundirty -> cleanUndirty
					whenMsg gotosanitized -> cleanSanitized

	State preDirty{
		[# Clean = Cleantime #]
		onMsg(startcleaner : startcleaner(T)){
			[# Table = payloadArg(0) #]
		}
	}
	Goto cleanDirty
	
	State preUndirty{
		[# Clean = Cleantime #]
	}
	Goto cleanUndirty
	
	State preSanitized{
		[# Clean = Cleantime #]
	}
	Goto cleanSanitized
	
	State cleanDirty { //Inizio catena di clean
		println("waitercleaner   |||   cleanDirty")
		onMsg(isTableStopped : isTableStopped(T)){
			replyTo isTableStopped with isTableStoppedDone : isTableStoppedDone(0)
		}
		else {
			forward timer -m starttimer : starttimer($Clean)
		}
	}
	Transition t0 whenMsg endtime -> preUndirty
				whenMsg stopcleaner -> timerStop
				whenRequest isTableStopped -> cleanDirty

	State cleanUndirty{
		updateResource[# "dirty" #]
		println("waitercleaner   |||   cleanUndirty")
		onMsg(isTableStopped : isTableStopped(T)){
			replyTo isTableStopped with isTableStoppedDone : isTableStoppedDone(0)
		}
		else{
			forward teatables -m setTableState : setTableState($Table,undirty) //I forward andrebbero fatti solo una volta, cosï¿½
			//invece vengono fatti ogni volta che resumiamo.
			forward timer -m starttimer : starttimer($Clean)
		}
	}
	Transition t0 whenMsg endtime -> preSanitized
				whenMsg stopcleaner -> timerStop
				whenRequest isTableStopped -> cleanUndirty

	State cleanSanitized{
		updateResource[# "undirty" #]
		println("waitercleaner   |||   cleanSanitized")
		onMsg(isTableStopped : isTableStopped(T)){
			replyTo isTableStopped with isTableStoppedDone : isTableStoppedDone(0)
		}
		else{
			forward teatables -m setTableState : setTableState($Table,sanitized)
			forward timer -m starttimer : starttimer($Clean)
		}

	}
	Transition t0 whenMsg endtime -> cleanDone
				whenMsg stopcleaner -> timerStop
				whenRequest isTableStopped -> cleanSanitized

	State cleanDone{ //Messaggio al waitermind di "fine"
	updateResource[# "clean" #]
		forward teatables -m clean : clean($Table)
		forward waitermind -m cleanerdone : cleanerdone(done)
	}
	Goto wait

}

QActor teatables context ctxtearoom{ 
[#
	var StateOfTables = ""
	var E = ""
	var Table = 0
#]
	State s0 initial{
		println("teatables START")
		solve( consult("tearoomkb.pl")	 )
	}
	Goto work
	
	State work{
		solve( stateOfTeatables(S) )
		ifSolved { [# StateOfTables = getCurSol("S").toString() #] }
		println("teatables s0: $StateOfTables")
		updateResource [# StateOfTables #]
	}
	Transition t0  whenMsg occupy -> engageTable
					whenMsg clean -> cleanTable
					whenRequest getTable -> getTable
					whenRequest tableClean -> tableClean
					whenMsg release -> release
				whenRequest isClean -> replyClean
					whenMsg setTableState -> setState
					whenRequest tableState -> tableState
					
	State getTable{
		onMsg(getTable : getTable(ID)){
			solve( getState(N, occupy($payloadArg(0))))
			ifSolved{
				[# Table = getCurSol("N").toString().toInt() #]
				replyTo getTable with tableId : tableId($Table)
			}
		}
	}
	Goto work
	
	State tableClean{
		solve(tableclean(N))
		ifSolved{
			[# Table = getCurSol("N").toString().toInt() #]
			replyTo tableClean with table : table($Table)
		}
		else{
			replyTo tableClean with table : table(0)
		}
	}
	Goto work
					
	State release {
		updateResource[# "clean" #]
		println("teatables   |||   release")
		printCurrentMessage
		onMsg(release : release(N)){
			solve( releaseTable($payloadArg(0)))
		}
	}
	Goto work

	State tableState{
		println("teateables   |||   tableState")
		printCurrentMessage
		onMsg(tableState : tableState(N)){
			[# Table = payloadArg(0).toString().toInt() #]
			solve( getState($Table,S))
			ifSolved{
				[# E = getCurSol("S").toString() #]
			}
			replyTo tableState with state : state($Table,$E)
		}
	}
	Goto work
					
	State setState{
		println("teatables   |||   setState")
		
		printCurrentMessage
		onMsg(setTableState : setTableState(N,S)){
			updateResource[# "${payloadArg(1)}" #]
			solve( setState($payloadArg(0),$payloadArg(1)) )
			ifSolved{
				solve( getState($payloadArg(0),S) )
				ifSolved{
					[#
						println(getCurSol("S").toString())
					#]
				}
			}
		}
	}
	Goto work
					
	State replyClean {
		println("teatables   |||   replyClean")
		printCurrentMessage
		onMsg(isClean : isClean(N)){
			[# Table = payloadArg(0).toString().toInt() #]
			println("onMsg isClean")
			solve( getState($Table,S) )
			ifSolved{
				[# 
					E = getCurSol("S").toString()
					if(E == "clean")
						E = "yes"
					else
						E = "no"
				#]
			}
			replyTo isClean with isCleanDone : isCleanDone($Table,$E)
			println("replyTo isClean $E")
		}
	}
	Goto work
					
	State engageTable{
		printCurrentMessage
		onMsg( occupy : occupy(N,ID) ){
			updateResource[# "occupy" #]
			println("engageTable ${payloadArg(0)}, ${payloadArg(1)}")
			solve( occupyTable( $payloadArg(0), $payloadArg(1)) )	//could do anything
			solve( stateOfTeatables(S) )
			[# StateOfTables = getCurSol("S").toString() #]  
			println("teatables engageTable ${payloadArg(0)}: $StateOfTables")
			updateResource [# StateOfTables #]
		}
	}
	Goto work

	State cleanTable{
		updateResource[# "clean" #]
		printCurrentMessage
		onMsg( clean : clean(N) ){
			println("clean ${payloadArg(0)}")
			solve( cleanTable( $payloadArg(0) ) )	//could do anything
			solve( stateOfTeatables(S) )
			[# StateOfTables = getCurSol("S").toString() #]  
			println("teatables cleanTable ${payloadArg(0)}: $StateOfTables")
			updateResource [# StateOfTables #] 			 
		}
	}
	Goto work
}

QActor waiterengine context ctxtearoom {
	[#
		var XP = "0"
		var YP = "0"
		var CurMove = ""
		val inmapname  = "teaRoomExplored"
		var StepTime    	   = 348L
		val BackTime           = 2 * StepTime / 3
	#]
	State s0 initial{
		discardMsg Off
		run itunibo.planner.plannerUtil.initAI() //init planner entity
		run itunibo.planner.plannerUtil.loadRoomMap( inmapname ) 		
		run itunibo.planner.plannerUtil.showCurrentRobotState()	
		println("waiterengine   |||   init")
	}
	Transition t0 whenRequest start -> started
					

	State started{
		println("waiterengine   |||   started")
		replyTo start with ready : ready(waiterengine)
	}
	Goto wait
	
	State wait{
		println("waiterengine   |||   wait")	
	}
	Transition t0 whenMsg moveto -> planmove
				whenMsg stopengine -> wait

	State planmove{
		onMsg( moveto : moveto(X,Y) ){
			[# XP = payloadArg(0)
			   YP = payloadArg(1)			  
			 #]
			println("waiterengine   |||   moveto ($XP,$YP)")
			run itunibo.planner.plannerUtil.planForGoal("$XP","$YP")
		}
	}
	Goto execPlanMove

	State execPlanMove{
		[#  CurMove = itunibo.planner.plannerUtil.getNextPlannedMove() #]
	}
	Transition t0 whenTime 100 -> nextStep
				whenMsg stopengine -> stop
	
	State nextStep{
		
	}
	Goto execStep if [# CurMove == "w" #] else execMove

	State stop{
		println("waiterengine   |||   stop")
		run itunibo.planner.plannerUtil.resetActions()
	}
	Goto wait
	
	State execStep {
		request basicrobot -m step : step( $StepTime )
	}
	Transition t0   whenReply stepdone   -> stepDone
					whenReply stepfail   -> stepFailed
	
	State stepDone{
		updateResource [# itunibo.planner.plannerUtil.getMapOneLine() #]
		run itunibo.planner.plannerUtil.updateMap( "w" )
  	}
	Goto execPlanMove if [# CurMove.length > 0 #] else endSuccess

	State stepFailed{
 		println("waiterengine | stepFailed")
		onMsg(stepfail : stepfail(DURATION, CAUSE) ){
			[# val D = payloadArg(0).toLong()  ; val Dt = Math.abs(StepTime-D); val BackT = D/2 #] 
			println("waiterengine stepFail D= $D, BackTime = ${BackTime}")
			if [# D > BackTime #] {
				forward basicrobot -m cmd : cmd( s )
				delayVar BackTime
				forward basicrobot -m cmd : cmd( h )
			}	
 		}
 	}  
	Goto execPlanMove if [# CurMove.length > 0 #] else endSuccess

	State execMove{
		forward basicrobot -m cmd : cmd($CurMove)
 		run itunibo.planner.plannerUtil.updateMap( "$CurMove" )
 		delay 100
	}
	Goto execPlanMove if [# CurMove.length > 0 #] else endSuccess

	State endSuccess{
		updateResource [# "($XP,$YP)" #]
		println("waiterengine   |||   endSuccess, curpos=($XP,$YP)")
		run itunibo.planner.plannerUtil.showCurrentRobotState()
		forward waitermind -m done : done($XP,$YP)
	}
	Goto wait
} 

QActor barman context ctxtearoom {
	
	[# 
		val Servicetime = 3000L
		var CurOrder = ""
		var CurCID = 0
	#]
	State s0 initial{
		println("barman   |||   init")
	}
	Goto wait
	
	State wait{
		println("barman   |||   wait")
		updateResource[# "waiting" #]
	}
	Transition t0 whenMsg order -> prepare
	
	State prepare{
		println("barman   |||   prepare")
		updateResource[# "preparing" #]
		onMsg(order : order(ID,O)){
			[#
				CurCID = payloadArg(0).toString().toInt()
				CurOrder = payloadArg(1).toString()	
			#]
		}
	}
	Transition t0 whenTimeVar Servicetime -> done
	
	State done{
		println("barman   |||   drinkReady")
		updateResource[# "drinkready" #]
		forward waitermind -m drinkready : drinkready($CurCID,$CurOrder)
	}
	Goto wait
}

QActor smartbell context ctxtearoom{
	[#
		val tmax = 37.5
		var IDRange = 1
		var CTemp:Double = 0.0
	#]
	State s0 initial{
		println("smartbell   |||   init")
	}
	Goto wait
	
	State wait{
		println("smartbell   |||   wait")
	}
	Transition t0 whenRequest enter -> checkTemperature
	
	State checkTemperature{
		[# CTemp = kotlin.random.Random.nextDouble(35.0,40.0) #]
		if[# CTemp < tmax #]{
			println("smartbell   |||   temp = $CTemp, client accepted")
			request waitermind -m enter : enter($IDRange)
			[# IDRange++ #]
		} else {
			println("smartbell   |||   temp = $CTemp, client rejected")
			forward smartbell -m reject : reject(reject)
		}
	}
	Transition t0 whenMsg reject -> rejected
			      whenReply accept -> done
	
	State done{
		onMsg(accept : accept(C)){
			if[# payloadArg(0) == "0" #]{
				println("smartbell   |||   tearoom is full, please wait...")
				replyTo enter with accept : accept(0)
			} else {
				println("smartbell   |||   welcome!")
				replyTo enter with accept : accept($payloadArg(0))
			}
		}
	}
	Goto wait

	State rejected{
		println("smartbell   |||   client temperatur is >= 37.5, go home!")
		[# CTemp = -1.0 #]
		replyTo enter with accept : accept($CTemp)
	}
	Goto wait
}

QActor maxstaytimer context ctxtearoom {
	[# 
		var Table = 0
		var Action = ""
		var RemTime1 = 0
		var RemTime2 = 0
	#]
	State s0 initial{
		println("maxstaytimer   |||   init")
	}
	Goto wait

	State wait{
		println("maxstaytimer   |||   wait")
	}
	Transition t0 whenMsg doTimerAction -> doTimerAction
					whenRequest getRemainingTime -> remTime1
					whenMsg timePassed -> timePassed

	State doTimerAction{
		println("maxstaytimer   |||   doTimerAction")
		onMsg(doTimerAction : doTimerAction(N,A)){
			[# 
				Table = payloadArg(0).toString().toInt()
				Action = payloadArg(1)
			#]
			if[# Table==1 #]{
				if[# Action=="start" #]{
					forward staytimertable1 -m starttimer : starttimer(start)
				}
				else {
					if [# Action=="stop" #]{
						forward staytimertable1 -m stopstaytimer : stopstaytimer(stop)
					}
					else{
						forward staytimertable1 -m timeroff : timeroff(off)
					}
				}
			}
			else{
				if[# Action=="start" #]{
					forward staytimertable2 -m starttimer : starttimer(start)
				}
				else {
					if [# Action=="stop" #]{
						forward staytimertable2 -m stopstaytimer : stopstaytimer(stop)
					}
					else{
						forward staytimertable2 -m timeroff : timeroff(off)
					}
				}
			}
		}
	}
	Goto wait
	
	State remTime1{
		request staytimertable1 -m getRemainingTime : getRemainingTime(time)
			
	}
	Transition t0 whenReply remainingTime -> remTime2

	State remTime2{
		onMsg(remainingTime : remainingTime(T)){
			[# RemTime1 = payloadArg(0).toString().toInt() #]
		}
		request staytimertable2 -m getRemainingTime : getRemainingTime(time)
	}
	Transition t0 whenReply remainingTime -> forwardRemainingTime
	
	State forwardRemainingTime{
		onMsg(remainingTime : remainingTime(T)){
			[# RemTime2 = payloadArg(0).toString().toInt() #]
			if[# RemTime1 <= RemTime2 #]{
				replyTo getRemainingTime with remainingTime : remainingTime($RemTime1)
			}
			else{
				replyTo getRemainingTime with remainingTime : remainingTime($RemTime2)
			}
		}
	}
	Goto wait
	
	State timePassed{
		println("maxstaytimer   |||   timePassed")
		onMsg(timePassed : timePassed(N)){
			[# Table = payloadArg(0).toString().toInt() #]
			forward waitermind -m timePassed : timePassed($Table)
		}
	}
	Goto wait
}

QActor staytimertable1 context ctxtearoom{
	[# 
		val MaxStayTime = 60*10L //In secondi
		var TimePassed = 0L
		var RemTime = 0L
	#]
	State s0 initial {
		discardMsg On
		println("staytimertable1   |||   init")
	}
	Goto wait
	
	State wait {
		printCurrentMessage
		onMsg(getRemainingTime : getRemainingTime(N)){
			[# RemTime = MaxStayTime - TimePassed #]
			replyTo getRemainingTime with remainingTime : remainingTime($RemTime)
		}
		println("staytimertable1   |||   wait")
	}
	Transition t0 
			whenMsg starttimer -> start
			whenRequest getRemainingTime -> wait
			
	State start{
		onMsg(starttimer : starttimer(T)){
			println("staytimertable1   |||   start")
		}
		[# TimePassed++ #]
		if[# TimePassed>=MaxStayTime #]{
			forward staytimertable1 -m endtime : endtime($TimePassed)
		}
	}

	Transition t0 whenTime 1000 -> start
				whenRequest getRemainingTime -> replyTimeStart
				whenMsg stopstaytimer -> wait
				whenMsg endtime -> timeFinish
				whenMsg timeroff -> end
				
	State replyTimeStart{
		onMsg(getRemainingTime : getRemainingTime(N)){
			[# RemTime = MaxStayTime - TimePassed #]
			replyTo getRemainingTime with remainingTime : remainingTime($RemTime)
		}
	}
	Goto start
	
	State end{
		[# RemTime = 0L #]
		[# TimePassed = 0L #]
		println("staytimertable1   |||   ended")
	}
	Goto wait
	
	State timeFinish{
		[# RemTime = 0L #]
		[# TimePassed = 0L #]
		println("maxstaytimer   |||   timeFinish")
		forward maxstaytimer -m timePassed : timePassed(1)
	}
	Goto wait
}

QActor staytimertable2 context ctxtearoom{
	[# 
		val MaxStayTime = 60*10L //In secondi
		var TimePassed = 0L
		var RemTime = 0L
	#]
	State s0 initial {
		discardMsg On
		println("staytimertable2   |||   init")
	}
	Goto wait
	
	State wait {
		printCurrentMessage
		println("staytimertable2   |||   wait")
	}
	Transition t0 whenMsg starttimer -> start
	
	State start{
		onMsg(starttimer : starttimer(T)){
			println("staytimertable2   |||   start")
		}
		[# TimePassed++ #]
		if[# TimePassed>=MaxStayTime #]{
			forward staytimertable2 -m endtime : endtime($TimePassed)
		}
	}
	Transition t0 whenTime 1000 -> start
				whenRequest getRemainingTime -> replyTimeStart
				whenMsg stopstaytimer -> wait
				whenMsg endtime -> timeFinish
				whenMsg timeroff -> end
				
	State replyTimeStart{
		onMsg(getRemainingTime : getRemainingTime(N)){
			[# RemTime = MaxStayTime - TimePassed #]
			replyTo getRemainingTime with remainingTime : remainingTime($RemTime)
		}
	}
	Goto start
	
	State end{
		[# TimePassed = 0L #]
		[# RemTime = 0L #]
		println("staytimertable2   |||   end")
	}
	Goto wait
	
	State timeFinish{
		println("staytimertable2   |||   timeFinish")
		[# RemTime = 0L #]
		[# TimePassed = 0L #]
		forward maxstaytimer -m timePassed : timePassed(2)
	}
	Goto wait
}

//QActor client context ctxtearoom {
//	State s0 initial{
//		println("client   |||   init")
//	}
//	Goto doSomething
//	
//	State doSomething{
//		println("client   |||   sending enter request")
//		request waitermind -m enter : enter(id)
//	}
//	Transition t0 whenReply accept -> ok
//	
//	State ok{
//		println("client   |||   accepted")
//		delay 15000
//		println("client   |||   ready")
//		forward waitermind -m clientready : clientready(id)
//		delay 15000
//		println("barman   |||   ready")
//		forward waitermind -m drinkready : drinkready(id)
//		delay 15000
//		forward waitermind -m paymentready : paymentready(id)
//		delay 10000
//		request waitermind -m enter : enter(id)
//		delay 20000
//		forward waitermind -m end : end(end)
//	}
//}